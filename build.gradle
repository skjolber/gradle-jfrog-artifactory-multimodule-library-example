plugins {
    id 'com.jfrog.artifactory' version '4.13.0' apply false
    id 'com.github.ben-manes.versions' version '0.28.0' apply false
    id 'org.owasp.dependencycheck' version '5.3.0' apply false
    id "com.github.spotbugs" version "3.0.0" apply false
}

// Split projects into buildable: examples and libraries (including frameworks). 
// This omits 'wrapper' directories. See also settings.gradle
 
def buildProjects() {
    subprojects.findAll { new File(it.projectDir, 'build.gradle').file  }
}

def exampleProjects() {
    buildProjects().findAll { it.name.endsWith("-example") || it.projectDir.path.substring(rootProject.projectDir.path.length()).contains('examples') }
}

def libraryProjects() {
    buildProjects().findAll { !exampleProjects().contains(it)  }
}

configure(exampleProjects()) {
    apply plugin: 'java'
}

configure(libraryProjects()) {
    apply plugin: 'java-library'
}

configure(buildProjects()) {
    apply plugin: 'com.jfrog.artifactory'
    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'org.owasp.dependencycheck'
    apply plugin: 'com.github.spotbugs'
    
    sourceCompatibility = 1.8
    
    ext {
        junitPlatformVersion = '1.6.0'
        junitJupiterVersion  = '5.6.0'
        googleTruthVersion   = '1.0.1'
    }
    
    test {
        useJUnitPlatform {
            includeEngines 'junit-jupiter'
        }
    
        reports {
            html.enabled = true
        }
    }

    dependencies {
        testCompile("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}")
        testRuntime("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")

        testCompile ("com.google.truth:truth:${googleTruthVersion}")
        testCompile ("com.google.truth.extensions:truth-java8-extension:${googleTruthVersion}")
    }
 }

// projects which should be published
configure(libraryProjects()) {
    apply plugin: 'maven-publish'

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
    
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }    
        
    artifactory {
        contextUrl = "${my_artifactory_contextUrl}" 
        publish {
            repository {
                repoKey = version.endsWith('SNAPSHOT') ? 'libs-snapshot-local' : 'libs-release-local'
                username = "${my_artifactory_user}"
                password = "${my_artifactory_password}"
                maven = true
            }
            defaults {
                publications ('mavenJava')
                publishBuildInfo = false
            }
        }    
    }
    
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
}
 
allprojects {
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    eclipse {
        classpath {
            downloadSources = true
            downloadJavadoc = true
        }
    }

    // Tell idea to output to build/classes/main instead of /out/
    idea {
        module {
            outputDir file('build/classes/main')
            testOutputDir file('build/classes/test')
        }
    }
}

artifactory {
    contextUrl = "${my_artifactory_contextUrl}" 
    resolve {
        repository {
            // if you are never working with multiple projects at once, this might as well
            // always be set to 'libs-release'
            repoKey = version.endsWith('SNAPSHOT') ? 'libs-snapshot' : 'libs-release'
            username = "${my_artifactory_user}"
            password = "${my_artifactory_password}"
            maven = true
        }
    }

    // publish build info only for the main project
    publish {
        repository {
            username = "${my_artifactory_user}"
            password = "${my_artifactory_password}"
        }
        defaults {
            publishArtifacts = false
        }
    }
}

